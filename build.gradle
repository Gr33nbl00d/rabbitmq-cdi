import java.text.SimpleDateFormat

plugins {
    id "java"
    id "signing"
    id "eclipse"
    id "jacoco"
    id "maven"
    id "org.asciidoctor.convert" version "1.5.7"
    id "net.nemerosa.versioning" version "2.7.1"
}

defaultTasks 'build'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}


configurations {
    asciidoclet
}

repositories {
    mavenCentral()
}

asciidoctorj { 
    version '1.5.6'
}

dependencies {
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.enterprise:cdi-api:1.2'
    compile 'javax.enterprise.concurrent:javax.enterprise.concurrent-api:1.0'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.rabbitmq:amqp-client:5.4.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.2'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    
    testRuntime 'junit:junit:4.12'
    testRuntime 'org.mockito:mockito-core:1.10.19'

    testRuntime 'ch.qos.logback:logback-classic:1.1.3'

    asciidoclet 'org.asciidoctor:asciidoclet:1.+'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { project.hasProperty('signing.password') && !project.version.contains('-SNAPSHOT') }
    sign configurations.archives
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RabbitMQ CDI extension',
                   'Implementation-Version': version,
                   'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                   'Build-Date': buildDate,
                   'Build-Time': buildTime,
                   'Build-Revision': versioning.info.commit,
                   'Specification-Title': project.name,
                   'Specification-Version': project.version,
                   'Specification-Vendor': 'reini.net',
                   'Automatic-Module-Name': "net.reini.rabbitmq.cdi"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

javadoc {
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "-attribute",
            "name=${project.name}," +
            "version=${project.version}," +
            "title-link=http://github.com/reinhapa/[${project.name} ${project.version}]"
}

uploadArchives {
    if (!project.hasProperty('signing.password')) {
        println "no archives will be uploaded due missing signing password"
        return
    }
    repositories.mavenDeployer {
        beforeDeployment { 
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
            name 'RabbitMQ JavaEE 7 CDI enabler'
            packaging 'jar'
            description 'Enables CDI based usage of RabbitMQ within a JavaEE 7 application.'
            url 'https://github.com/reinhapa/rabbitmq-cdi'
            
            scm {
                connection 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                developerConnection 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                url 'https://github.com/reinhapa/rabbitmq-cdi'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'reinhapa'
                    name 'Patrick Reinhart'
                    email 'patrick@reini.net'
                }
            }
        }
    }
}
